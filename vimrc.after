syntax enable
set background=dark
colorscheme solarized

" Run shell commands via sh
set shell=/bin/sh

" CtrlP Customizations
" Map C-\ to buffer search
if has("gui_macvim") && has("gui_running")
  call janus#add_mapping('ctrlp', 'map', '<D-\>', ':CtrlPBuffer<CR>')
  call janus#add_mapping('ctrlp', 'imap', '<D-\>', '<ESC>:CtrlPBuffer<CR>')
else
  call janus#add_mapping('ctrlp', 'map', '<C-\>', ':CtrlPBuffer<CR>')
  call janus#add_mapping('ctrlp', 'imap', '<C-\>', '<ESC>:CtrlPBuffer<CR>')
endif

" Search by file name instead of path
"let g:ctrlp_by_filename = 1

" Prevent NERDTree from opening buffer on startup
augroup AuNERDTreeCmd
  autocmd!
augroup end

" Allow custom GitHub repository server for :GBrowse
let g:fugitive_github_domains = ['source.xing.com']

" Use fancy characters in Powerline bar
let g:Powerline_symbols = 'fancy'

" Run rspec tests through vim
map <Leader>t :call RunCurrentSpecFile()<CR>
map <Leader>s :call RunNearestSpec()<CR>
map <Leader>l :call RunLastSpec()<CR>

function! RunCurrentSpecFile()
  if InSpecFile()
    let l:command = "bin/zspec " . @% . " -f documentation"
    call SetLastSpecCommand(l:command)
    call RunSpecs(l:command)
  endif
endfunction

function! RunNearestSpec()
  if InSpecFile()
    let l:command = "bin/zspec " . @% . ":" . line(".") . " -f documentation"
    call SetLastSpecCommand(l:command)
    call RunSpecs(l:command)
  endif
endfunction

function! RunLastSpec()
  if exists("t:last_spec_command")
    call RunSpecs(t:last_spec_command)
  endif
endfunction

function! InSpecFile()
  return match(expand("%"), "_spec.rb$") != -1
endfunction

function! SetLastSpecCommand(command)
  let t:last_spec_command = a:command
endfunction

function! RunSpecs(command)
  execute ":w\|!clear && echo " . a:command . " && echo && " . a:command
endfunction
